import basic.osc

scenario top:
    path: Path                 
    path.set_map("Town04")
    path.path_min_driving_lanes(2)

    ego_vehicle: Model3                 # ego car
    npc: Rubicon                        # The other car

    event start
    event end
    do serial:
        # Initial setup - both vehicles in same lane
        initial_position: parallel(duration: 3s):
            npc.drive(path) with: 
                speed(30kph)
                lane(2, at: start) # left to right: [1..n]
            ego_vehicle.drive(path) with:
                speed(30kph)
                lane(same_as: npc, at: start)
                position(50m, behind: npc, at: start)

        # Move to left lane for overtaking
        change_to_left: parallel(duration: 4s):
            npc.drive(path) with: 
                speed(30kph)
            ego_vehicle.drive(path) with:
                lane(left_of: npc, at: end)

        # Accelerate to overtake from left
        accelerate_left: parallel(duration: 6s):
            npc.drive(path) with: 
                speed(30kph)
            ego_vehicle.drive(path) with:
                speed(60kph)
                
        # Return to original lane after overtaking
        return_to_right: parallel(duration: 4s):
            npc.drive(path) with: 
                speed(30kph)
            ego_vehicle.drive(path) with:
                lane(same_as: npc, at: end)
                # position(100m, ahead: npc, at: end)
